apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def globalConfiguration = rootProject.extensions.getByName("ext")

android {
    compileSdkVersion build_versions.compile_sdk
    buildToolsVersion build_versions.build_tools

    testBuildType "debug"

    defaultConfig {
        applicationId "com.androidkotlincore.androidkotlincoresample"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        vectorDrawables.useSupportLibrary = true

        def versionMajor = 1
        def versionMinor = 0
        def versionPatch = 2
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        versionCode 15

        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        encoding "UTF-8"
    }
    kotlin { experimental { coroutines 'enable' } }

    buildTypes {
        release {

        }
        debug {

        }
    }


    applicationVariants.all { variant ->
        println("Iterating variant: " + variant.getName())
        switch (variant.getName()) {
            case 'release':
                variant.buildConfigField "String", "DB_NAME", "\"${globalConfiguration['databaseNameRelease']}\""
                break
            case 'debug':
                variant.buildConfigField "String", "DB_NAME", "\"${globalConfiguration['databaseNameDev']}\""
                break
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //modules
    implementation deps.entityconverter

    //support
    implementation deps.support.appcompat
    implementation deps.support.design
    implementation deps.support.cardview
    implementation deps.support.annotations
    implementation deps.support.recyclerview
    implementation deps.support.constraintLayout

    //glide
    implementation deps.glide.glide
    annotationProcessor deps.glide.glideAnno
    implementation deps.glide.glideTransformers
    implementation deps.glide.glideOkHttp
    kapt deps.glide.glideCompiler

    //play services
    implementation deps.playServices.maps
    implementation deps.playServices.locations

    //networking
    implementation deps.network.okhttp
    implementation deps.network.logging
    implementation deps.network.retrofit
    implementation deps.network.rxadapter
    implementation deps.network.gsonconverter

    //rx
    implementation deps.rx.android
    implementation deps.rx.java

    //coroutines
    implementation deps.coroutines.core
    implementation deps.coroutines.android
    implementation deps.coroutines.rx

    //kotlin
    implementation deps.kotlin.stdlib

    //database
    implementation deps.database.roomRuntime
    implementation deps.database.roomRxAdapter
    kapt deps.database.roomAnnotationProcessor

    //debugging utils
    debugImplementation deps.other.leak

    //dagger
    implementation deps.dagger.lib
    kapt deps.dagger.processor
    implementation deps.dagger.android
    kapt deps.dagger.androidProcessor
    implementation deps.dagger.support
    kaptAndroidTest deps.dagger.processor
}
